#
# Check a printer for conformance with PWG 5100.11-2023 (EPX)
#
#
# Usage:
#
#  ./ipptool -f FILENAME printer-uri pwg5100.11.test
#

################################################################################

#---------------------------- REQUIRED ATTRIBUTES -----------------------------#

################################################################################
{
  NAME "[EPX2 1.1] Required Attributes"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'printer-service-contact-col'

  STATUS successful-ok

  EXPECT "printer-service-contact-col"  OF-TYPE collection|unknown IN-GROUP printer-attributes-tag
  EXPECT "printer-service-contact-col"  OF-TYPE collection IN-GROUP printer-attributes-tag DEFINE-MATCH HAS_PRINTER_SERVICE_CONTACT_COL_COLLECTION
}
################################################################################
{
  NAME "[EPX2 1.2] Feature Detection"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'ipp-features-supported'

  STATUS successful-ok

  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag
  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-release' DEFINE-MATCH HAS_JOB_RELEASE
  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage' DEFINE-MATCH HAS_JOB_STORAGE
  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'print-policy' DEFINE-MATCH HAS_PRINT_POLICY
  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'proof-and-suspend' DEFINE-MATCH HAS_PROOF_AND_SUSPEND
  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'proof-print' DEFINE-MATCH HAS_PROOF_PRINT
}
################################################################################

#------------------------ printer-service-contact-col -------------------------#

################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PRINTER_SERVICE_CONTACT_COL_COLLECTION

  NAME "[EPX2 2.1] printer-service-contact-col collection"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'printer-service-contact-col'

  STATUS successful-ok

  EXPECT "printer-service-contact-col"  OF-TYPE collection IN-GROUP printer-attributes-tag
  EXPECT "printer-service-contact-col/contact-name"  OF-TYPE name
  EXPECT "printer-service-contact-col/contact-uri"  OF-TYPE uri
}
################################################################################

#-------------------------------- JOB RELEASE ---------------------------------#

################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_RELEASE

  NAME "[EPX2 3.1] Job Release: Method Detection"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'ipp-features-supported','operations-supported','job-release-action-supported'

  STATUS successful-ok

  EXPECT "ipp-features-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-release'
  EXPECT "operations-supported" OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x000D # Release-Job

  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-password' DEFINE-MATCH HAS_JOB_RELEASE_JOB_PASSWORD
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'button-press' DEFINE-MATCH HAS_JOB_RELEASE_BUTTON_PRESS
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner-authorized' DEFINE-MATCH HAS_JOB_RELEASE_OWNER_AUTHORIZED
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'none' DEFINE-MATCH HAS_JOB_RELEASE_NONE

  # Attempt to express that at least one of 'job-password' / 'button-press' / 'owner-authorized' must be supported?
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-password' DEFINE-MATCH HAS_JOB_RELEASE_REQ_MET
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'button-press' DEFINE-MATCH HAS_JOB_RELEASE_REQ_MET
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner-authorized' DEFINE-MATCH HAS_JOB_RELEASE_REQ_MET
  PASS-IF-DEFINED HAS_JOB_RELEASE_REQ_MET
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_RELEASE_BUTTON_PRESS

  NAME "[EPX2 3.2.1] Job Release: Button Press Method Requirements"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'job-release-action-default','job-spooling-supported','job-release-action-supported'

  STATUS successful-ok

  # These are common to all methods other than 'none'
  EXPECT "job-release-action-default" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
  EXPECT "job-spooling-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'

  # These are specific to this particular sub-feature
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'button-press'
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_RELEASE_BUTTON_PRESS

  NAME "[EPX2 3.2.2] Job Release: Create Button Press Release Job"
  OPERATION Print-Job

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR name "requesting-user-name" $user
    ATTR mimeMediaType "document-format" $filetype

  GROUP job-attributes-tag
    ATTR name "job-name" "Job Release Button Press Test $job-id $filename"
    ATTR keyword "job-release-action" 'button-press'

  FILE $filename

  STATUS successful-ok

  EXPECT "job-id" OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1
  EXPECT "job-uri" OF-TYPE uri IN-GROUP job-attributes-tag COUNT 1
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_RELEASE_JOB_PASSWORD

  NAME "[EPX2 3.3.1] Job Release: Password Method Requirements"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'job-release-action-default','job-spooling-supported','job-release-action-supported','job-password-encryption-supported','job-password-length-supported','job-password-repertoire-configured','job-password-repertoire-supported','job-password-supported'

  STATUS successful-ok

  # These are common to all methods other than 'none'
  EXPECT "job-release-action-default" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
  EXPECT "job-spooling-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'

  # These are specific to this particular sub-feature
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-password'

  EXPECT "job-password-encryption-supported"    OF-TYPE keyword IN-GROUP printer-attributes-tag
  EXPECT "job-password-length-supported"        OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag
  EXPECT "job-password-repertoire-configured"   OF-TYPE keyword IN-GROUP printer-attributes-tag
  EXPECT "job-password-repertoire-supported"    OF-TYPE keyword IN-GROUP printer-attributes-tag
  EXPECT "job-password-supported"               OF-TYPE integer IN-GROUP printer-attributes-tag
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_RELEASE_OWNER_AUTHORIZED

  NAME "[EPX2 3.4.1] Job Release: Owner Authorized Method Requirements"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'job-release-action-default','job-spooling-supported','job-release-action-supported','uri-authentication-supported'

  STATUS successful-ok

  # These are common to all methods other than 'none'
  EXPECT "job-release-action-default" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
  EXPECT "job-spooling-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'

  # These are specific to this particular sub-feature
  EXPECT "job-release-action-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner-authorized'

  # TODO: Do we require some types of auth or disallow others?
  EXPECT "uri-authentication-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag

  # These are common to all methods other than 'none'
  EXPECT "job-release-action-default" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM "job-release-action-supported" COUNT 1
  EXPECT "job-spooling-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'
}
################################################################################

#----------------------------- JOB PRINT POLICY -------------------------------#

################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PRINT_POLICY

  NAME "[EPX2 4.1.1] Job Print Policy: REQUIRED attributes"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'operations-supported'

  STATUS successful-ok

  EXPECT "operations-supported" OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0066 # Get-User-Printer-Attributes
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PRINT_POLICY

  NAME "[EPX2 4.1.2] Job Print Policy: Get-User-Printer-Attributes"
  OPERATION Get-User-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR name "requesting-user-name" $user
    ATTR keyword "requested-attributes" 'document-format-supported','sides-supported','sides-default','color-supported','print-quality-supported','print-quality-default'

  STATUS successful-ok

  EXPECT "document-format-supported" OF-TYPE mimeMediaType IN-GROUP printer-attributes-tag
  EXPECT "sides-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'two-sided-long-edge'
  EXPECT "sides-default" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM "sides-supported" COUNT 1
  EXPECT "color-supported" OF-TYPE boolean IN-GROUP printer-attributes-tag WITH-VALUE false
  EXPECT "print-quality-default" OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3 # draft
}
################################################################################

#-------------------------------- JOB STORAGE ---------------------------------#

################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_STORAGE

  NAME "[EPX2 5.1] Job Storage - Feature Detection"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'job-storage-supported','job-storage-access-supported','job-storage-disposition-supported'

  STATUS successful-ok
  
  # Required attributes and values
  EXPECT "job-storage-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-access'
  EXPECT "job-storage-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-disposition'
  EXPECT "job-storage-access-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'public'
  EXPECT "job-storage-access-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner'
  EXPECT "job-storage-disposition-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'print-and-store'
  EXPECT "job-storage-disposition-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'store-only'
  
  # Detect optional features
  EXPECT "job-storage-supported" OF-TYPE name IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-group' DEFINE-MATCH HAS_JOB_STORAGE_GROUP
  EXPECT "job-storage-access-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'group' DEFINE-MATCH HAS_JOB_STORAGE_GROUP


}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_STORAGE_GROUP

  NAME "[EPX2 5.2] Job Storage - Validate Group Feature Support"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'job-storage-supported','job-storage-access-supported','job-storage-group-supported'

  STATUS successful-ok
  
  # Required attributes
  EXPECT "job-storage-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-group'
  EXPECT "job-storage-access-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'group'
  EXPECT "job-storage-group-supported" OF-TYPE name IN-GROUP printer-attributes-tag WITH-DISTINCT-VALUES
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_JOB_STORAGE

  NAME "[EPX2 5.3.1] Job Storage - Create public 'print-and-store' Job"
  OPERATION Print-Job

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR name "requesting-user-name" $user
    ATTR mimeMediaType "document-format" $filetype

  GROUP job-attributes-tag
    ATTR name "job-name" "Job Storage Test $filename $job-id"
    ATTR collection "job-storage"
    {
      MEMBER keyword "job-storage-access" 'public'
      MEMBER keyword "job-storage-disposition" 'print-and-store'
    }

  FILE $filename

  MONITOR-PRINTER-STATE $uri {
    EXPECT printer-state WITH-VALUE '3'
  }

  STATUS successful-ok

  EXPECT "job-id" OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1
  EXPECT "job-id" OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1 DEFINE-MATCH HAS_ACCEPTED_STORED_JOB_1
  EXPECT "job-uri" OF-TYPE uri IN-GROUP job-attributes-tag COUNT 1
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_ACCEPTED_STORED_JOB_1

  NAME "[EPX2 5.3.2] Job Storage - Check Stored Job"
  OPERATION Get-Job-Attributes
  
  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR naturalLanguage "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR integer "job-id" $job-id
    ATTR name "requesting-user-name" $user

  STATUS successful-ok

  # Expecting the Job to be processed to produce the number of "proof-copies"
  # and then the Job is placed in the 'processing-stopped' state (6)
  EXPECT "job-impressions-completed" OF-TYPE integer WITH-VALUE $PROOF_COPIES
  EXPECT "job-id" OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE $job-id DEFINE-MATCH STORED_JOB_1_JOB_ID
  EXPECT "job-state" OF-TYPE enum IN-GROUP job-attributes-tag COUNT 1 WITH-VALUE =9 REPEAT-NO-MATCH REPEAT-LIMIT 30
  DISPLAY "job-state"
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_ACCEPTED_STORED_JOB_1

  NAME "[EPX2 5.3.3] Job Storage - Resubmit-Job"
  OPERATION Resubmit-Job

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR integer job-id $job-id
    ATTR name "requesting-user-name" $user

  STATUS successful-ok
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_ACCEPTED_STORED_JOB_1

  NAME "[EPX2 5.3.4] Job Storage - Wait For terminal state"
  OPERATION Get-Job-Attributes

  GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR integer job-id $job-id
    ATTR name "requesting-user-name" $user

  STATUS successful-ok

  EXPECT !"job-storage" OF-TYPE collection
  EXPECT "job-state" OF-TYPE unknown|enum IN-GROUP job-attributes-tag COUNT 1 WITH-VALUE >6 REPEAT-NO-MATCH REPEAT-LIMIT 30
  EXPECT "job-state-reasons" OF-TYPE keyword IN-GROUP job-attributes-tag
  DISPLAY "job-state"
}
################################################################################

#--------------------------- JOB PROOF AND SUSPEND ----------------------------#

################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PROOF_AND_SUSPEND

  NAME "[EPX2 6.1] Job Proof and Suspend: REQUIRED attributes"
  OPERATION Get-Printer-Attributes

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR keyword "requested-attributes" 'operations-supported','proof-copies-supported','job-spooling-supported'

  STATUS successful-ok

  EXPECT "operations-supported"   OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x002F # Resume-Job
  EXPECT "proof-copies-supported" OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag
  EXPECT "job-spooling-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'
  EXPECT "job-spooling-supported" OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool' DEFINE-MATCH HAS_SPOOLING
}
################################################################################

DEFINE PROOF_COPIES_2 2

{
  SKIP-IF-NOT-DEFINED HAS_PROOF_AND_SUSPEND
  SKIP-IF-NOT-DEFINED HAS_SPOOLING

  NAME "[EPX2 6.2.1] Job Proof and Suspend - Create Proof Job"
  OPERATION Print-Job

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR name "requesting-user-name" $user
    ATTR mimeMediaType "document-format" $filetype

  GROUP job-attributes-tag
    ATTR name "job-name" "Job Proof and Suspend Test $filename $job-id"
    ATTR integer "copies" 5
    ATTR integer "proof-copies" $PROOF_COPIES_2

  FILE $filename

  STATUS successful-ok

  EXPECT "job-id" OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1
  EXPECT "job-id" OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1 DEFINE-MATCH HAS_ACCEPTED_PROOF_JOB
  EXPECT "job-uri" OF-TYPE uri IN-GROUP job-attributes-tag COUNT 1
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PROOF_AND_SUSPEND
  SKIP-IF-NOT-DEFINED HAS_ACCEPTED_PROOF_JOB

  NAME "[EPX2 6.2.2] Job Proof and Suspend - Wait For 'processing-stopped'"
  OPERATION Get-Job-Attributes
  
  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR naturalLanguage "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR integer "job-id" $job-id
    ATTR name "requesting-user-name" $user

  STATUS successful-ok

  # Expecting the Job to be processed to produce the number of "proof-copies"
  # and then the Job is placed in the 'processing-stopped' state (6)
  EXPECT "proof-copies" OF-TYPE integer WITH-VALUE $PROOF_COPIES_2 COUNT 1
  EXPECT "job-impressions-completed" OF-TYPE integer WITH-VALUE $PROOF_COPIES
  EXPECT "job-state" OF-TYPE unknown|enum IN-GROUP job-attributes-tag COUNT 1 WITH-VALUE =6 REPEAT-NO-MATCH REPEAT-LIMIT 30
  DISPLAY "job-state"
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PROOF_AND_SUSPEND

  NAME "[EPX2 6.2.3] Job Proof and Suspend - Resume-Job"
  OPERATION Resume-Job

  GROUP operation-attributes-tag
    ATTR charset "attributes-charset" utf-8
    ATTR language "attributes-natural-language" en
    ATTR uri "printer-uri" $uri
    ATTR integer job-id $job-id
    ATTR name "requesting-user-name" $user

  STATUS successful-ok
}
################################################################################
{
  SKIP-IF-NOT-DEFINED HAS_PROOF_AND_SUSPEND

  NAME "[EPX2 6.2.4] Job Proof and Suspend - Wait For terminal state"
  OPERATION Get-Job-Attributes

  GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR integer job-id $job-id
    ATTR name "requesting-user-name" $user

  STATUS successful-ok

  EXPECT "proof-copies" OF-TYPE integer WITH-VALUE $PROOF_COPIES_2 COUNT 1
  # Expecting the Job to be processed to produce the number of "proof-copies"
  # and then the Job is placed in the 'processing-stopped' state (6)
  EXPECT "job-state" OF-TYPE unknown|enum IN-GROUP job-attributes-tag COUNT 1 WITH-VALUE >6 REPEAT-NO-MATCH REPEAT-LIMIT 30
  DISPLAY "job-state"
}
################################################################################

