#########################################################
#
# Test a printer for conformance with PWG 5100.11 (EPX)
#
# Usage:
#   ./ipptool -f FILENAME printer-uri pwg5100.11.test
#
#########################################################

DEFINE datecodelong 19700101000000
DEFINE IPP_URI_SCHEME "/^ipps?://.+$$/"

#########################################################
{
    NAME "[EPX2-1.1] Required Attributes"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'printer-service-contact-col','document-format-supported'

    STATUS successful-ok
    
    EXPECT "printer-service-contact-col"  OF-TYPE collection|unknown IN-GROUP printer-attributes-tag
    EXPECT "printer-service-contact-col"  OF-TYPE collection IN-GROUP printer-attributes-tag DEFINE-MATCH HAVE_PRINTER_SERVICE_CONTACT_COL_COLLECTION
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_PRINTER_SERVICE_CONTACT_COL_COLLECTION

    NAME "[EPX2-1.2] printer-service-contact-col collection"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'printer-service-contact-col'

    STATUS successful-ok
    
    EXPECT printer-service-contact-col  OF-TYPE collection IN-GROUP printer-attributes-tag
    EXPECT printer-service-contact-col/contact-name  OF-TYPE name
    EXPECT printer-service-contact-col/contact-uri  OF-TYPE uri
    
}
#########################################################
{
    NAME "[EPX2-1.3] Feature Detection"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'ipp-features-supported'

    STATUS successful-ok
    
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-release' DEFINE-MATCH HAVE_JOB_RELEASE
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage' DEFINE-MATCH HAVE_JOB_STORAGE
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'print-policy' DEFINE-MATCH HAVE_PRINT_POLICY
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'proof-and-suspend' DEFINE-MATCH HAVE_PROOF_AND_SUSPEND
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'proof-print' DEFINE-MATCH HAVE_PROOF_PRINT
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_RELEASE

    NAME "[EPX2-2.1] Job Release: Method Detection"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'ipp-features-supported','job-release-action-supported'

    STATUS successful-ok
    
    EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-release'

    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-password' DEFINE-MATCH HAVE_JOB_RELEASE_JOB_PASSWORD
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'button-press' DEFINE-MATCH HAVE_JOB_RELEASE_BUTTON_PRESS
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner-authorized' DEFINE-MATCH HAVE_JOB_RELEASE_OWNER_AUTHORIZED
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'none' DEFINE-MATCH HAVE_JOB_RELEASE_NONE
    
    # Attempt to express that at least one of 'job-password' / 'button-press' / 'owner-authorized' must be supported?
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-password' DEFINE-MATCH HAVE_JOB_RELEASE_REQ_MET
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'button-press' DEFINE-MATCH HAVE_JOB_RELEASE_REQ_MET
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner-authorized' DEFINE-MATCH HAVE_JOB_RELEASE_REQ_MET
    PASS-IF-DEFINED HAVE_JOB_RELEASE_REQ_MET
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_RELEASE_BUTTON_PRESS

    NAME "[EPX2-2.2.1] Job Release: Button Press Method Requirements"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'job-release-action-default','job-spooling-supported','job-release-action-supported'

    STATUS successful-ok
    
    # These are common to all methods other than 'none'
    EXPECT job-release-action-default OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
    EXPECT job-spooling-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'

    # These are specific to this particular sub-feature
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'button-press'

}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_RELEASE_JOB_PASSWORD

    NAME "[EPX2-2.3.1] Job Release: Password Method Requirements"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'job-release-action-default','job-spooling-supported','job-release-action-supported','job-password-encryption-supported','job-password-length-supported','job-password-repertiore-configured','job-password-repertiore-supported','job-password-supported'

    STATUS successful-ok
    
    # These are common to all methods other than 'none'
    EXPECT job-release-action-default OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
    EXPECT job-spooling-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'

    # These are specific to this particular sub-feature
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-password'

    EXPECT job-password-encryption-supported    OF-TYPE keyword IN-GROUP printer-attributes-tag
    EXPECT job-password-length-supported        OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag
    EXPECT job-password-repertiore-configured   OF-TYPE keyword IN-GROUP printer-attributes-tag
    EXPECT job-password-repertiore-supported    OF-TYPE keyword IN-GROUP printer-attributes-tag
    EXPECT job-password-supported               OF-TYPE integer IN-GROUP printer-attributes-tag

}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_RELEASE_OWNER_AUTHORIZED

    NAME "[EPX2-2.4.1] Job Release: Owner Authorized Method Requirements"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'job-release-action-default','job-spooling-supported','job-release-action-supported','uri-authentication-supported'

    STATUS successful-ok
    
    # These are common to all methods other than 'none'
    EXPECT job-release-action-default OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
    EXPECT job-spooling-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'

    # These are specific to this particular sub-feature
    EXPECT job-release-action-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner-authorized'

    # TODO: Do we require some types of auth or disallow others?
    EXPECT uri-authentication-supported OF-TYPE keyword IN-GROUP printer-attributes-tag 

    # These are common to all methods other than 'none'
    EXPECT job-release-action-default OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-release-action-supported COUNT 1
    EXPECT job-spooling-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'spool'
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE

    NAME "[EPX2-3.1] Job Storage: REQUIRED attributes"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'job-storage-supported','job-storage-access-supported','job-storage-disposition-supported','which-jobs-supported','operations-supported'

    STATUS successful-ok
    
    # Required members of 'job-storage'
    EXPECT 'job-storage-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-access'
    EXPECT 'job-storage-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-disposition'
    EXPECT 'job-storage-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'job-storage-group' DEFINE-MATCH HAVE_JOB_STORAGE_GROUP

    EXPECT 'job-storage-access-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'owner'
    EXPECT 'job-storage-access-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'public'
    EXPECT 'job-storage-access-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'group' IF-DEFINED HAVE_JOB_STORAGE_GROUP

    EXPECT 'job-storage-disposition-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'print-and-store'
    EXPECT 'job-storage-disposition-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'store-only'

    EXPECT 'which-jobs-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'stored-owner'
    EXPECT 'which-jobs-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'stored-public'
    EXPECT 'which-jobs-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'stored-group' IF-DEFINED HAVE_JOB_STORAGE_GROUP

    EXPECT  'operations-supported' OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0005  # Create-Job
    EXPECT  'operations-supported' OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0006  # Send-Document
    EXPECT  'operations-supported' OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0008  # Cancel-Job
    EXPECT  'operations-supported' OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0009  # Get-Job-Attributes
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE

    NAME "[EPX2-3.2.1] Job Storage: Get-Jobs - which-jobs=stored-public"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR name requesting-user-name $user
        ATTR boolean my-jobs true

        ATTR keyword which-jobs stored-public

    STATUS successful-ok
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE

    NAME "[EPX2-3.2.2] Job Storage: Get-Jobs - which-jobs=stored-owner"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR name requesting-user-name $user
        ATTR boolean my-jobs true

        ATTR keyword which-jobs stored-owner

    STATUS successful-ok
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE_GROUP

    NAME "[EPX2-3.2.3] Job Storage: Get-Jobs - which-jobs=stored-group"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR name requesting-user-name $user
        ATTR boolean my-jobs true

        ATTR keyword which-jobs stored-group

    STATUS successful-ok
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE

    NAME "[EPX2-3.3.1] Job Storage: Create-Job for new Stored Job - Owner / Store Only"

    OPERATION Create-Job

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri

        ATTR name requesting-user-name $user
        ATTR name job-name "$user - $hostname - $filename - $datecodelong"
        ATTR collection "job-storage" {
          MEMBER keyword "job-storage-access"       'owner'
          MEMBER keyword "job-storage-disposition"  'store-only'
        }

    GROUP job-attributes-tag
        ATTR integer copies 1

    STATUS successful-ok

    EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE "$IPP_URI_SCHEME"
    EXPECT job-id OF-TYPE integer COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE >0
    EXPECT job-id DEFINE-VALUE STORED_JOB_TEST_JOB_1_ID
    EXPECT ?job-uuid
    EXPECT job-state OF-TYPE unknown|enum COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE 3,4,5,6
    EXPECT ?job-state-message OF-TYPE text IN-GROUP job-attributes-tag
    EXPECT job-state-reasons OF-TYPE keyword IN-GROUP job-attributes-tag
}
#########################################################
{
    SKIP-IF-NOT-DEFINED STORED_JOB_TEST_JOB_1_ID

    NAME "[EPX2-3.3.2] Job Storage: Send-Document - job-id=$STORED_JOB_TEST_JOB_1_ID"
    OPERATION Send-Document

    GROUP operation-attributes-tag
      ATTR charset attributes-charset utf-8
      ATTR language attributes-natural-language en
      ATTR uri job-uri $job-uri

      ATTR name requesting-user-name $user
      ATTR mimeMediaType document-format $filetype
      ATTR boolean last-document true

    FILE $filename

    STATUS successful-ok
    STATUS successful-ok
    STATUS server-error-job-canceled
    STATUS server-error-busy REPEAT-MATCH REPEAT-LIMIT 30

    EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE "$IPP_URI_SCHEME"
    EXPECT job-id OF-TYPE integer COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE >0
    EXPECT ?job-uuid
    EXPECT job-state OF-TYPE unknown|enum COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE 3,4,5,6
    EXPECT ?job-state-message OF-TYPE text IN-GROUP job-attributes-tag
    EXPECT job-state-reasons OF-TYPE keyword IN-GROUP job-attributes-tag
}
#########################################################
{
    NAME "[EPX2-3.3.3] Job Storage: Monitor Job until it reaches terminal state ($date-current)"
    IGNORE-ERRORS yes

    OPERATION Get-Job-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR integer job-id $job-id
        ATTR name requesting-user-name $user

    STATUS successful-ok

    EXPECT job-id
    EXPECT job-state OF-TYPE unknown|enum IN-GROUP job-attributes-tag COUNT 1 WITH-VALUE >6 REPEAT-NO-MATCH
    DISPLAY job-state
    DISPLAY job-state-reasons
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE

    NAME "[EPX2-3.3.4] Job Storage: Get-Jobs - which-jobs=stored-owner"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR name requesting-user-name $user
        ATTR boolean my-jobs true

        ATTR keyword which-jobs stored-owner

    STATUS successful-ok
}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_JOB_STORAGE

    NAME "[EPX2-3.3.5] Get-Job-Attributes Test"
    OPERATION Get-Job-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR naturalLanguage attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR integer job-id $JOB_ID
        ATTR name requesting-user-name $user

  STATUS successful-ok

}











#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_PROOF_AND_SUSPEND

    NAME "[EPX2-5.1] Job Proof and Suspend: REQUIRED attributes"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'proof-copies-supported','which-jobs-supported'

    STATUS successful-ok
    
    EXPECT 'proof-copies-supported' OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag
    EXPECT 'which-jobs-supported' OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE 'proof-and-suspend'

}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_PRINT_POLICY

    NAME "[EPX2-5.1] Print Policy: REQUIRED attributes"
    OPERATION Get-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes 'operations-supported'

    STATUS successful-ok
    
    EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0066 # Get-User-Printer-Attributes
    # TODO: Do we require some types of auth or disallow others?
    EXPECT uri-authentication-supported OF-TYPE keyword IN-GROUP printer-attributes-tag 

}
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_PRINT_POLICY

    NAME "[EPX2-5.2] Print Policy: Get-User-Printer-Attributes"
    OPERATION Get-User-Printer-Attributes

    GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes all,'media-col-ready','media-col-supported','finishings-col-ready','finishings-col-supported'

    STATUS successful-ok
    
    EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0066 # Get-User-Printer-Attributes
    # TODO: Do we require some types of auth or disallow others?
    EXPECT uri-authentication-supported OF-TYPE keyword IN-GROUP printer-attributes-tag 

}






#########################################################
#########################################################
#########################################################
{
    SKIP-IF-NOT-DEFINED HAVE_PROOF_PRINT

    NAME "[EPX2- print-job with proof-print"
    OPERATION Print-Job

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR language attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR mimeMediaType document-format $filetype

    GROUP job-attributes-tag
    ATTR integer copies 1
    ATTR collection proof-print {
        MEMBER integer proof-print-copies 1
        MEMBER keyword media letterhead
    }

    FILE $filename

    STATUS successful-ok

    EXPECT job-id OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1
    EXPECT job-uri OF-TYPE uri IN-GROUP job-attributes-tag COUNT 1
}

# Wait for job to complete...

{
    SKIP-IF-NOT-DEFINED HAVE_PROOF_PRINT

    NAME "[EPX2- Get-Job-Attributes Until Job Complete"
    OPERATION Get-Job-Attributes
    
    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR integer job-id $job-id
    ATTR name requesting-user-name $user

    STATUS successful-ok

    EXPECT proof-print OF-TYPE collection IN-GROUP job-attributes-tag COUNT 1
    EXPECT proof-print/proof-print-copies OF-TYPE integer WITH-VALUE 1 COUNT 1
    EXPECT proof-print/media OF-TYPE keyword WITH-VALUE letterhead COUNT 1
    EXPECT job-state OF-TYPE unknown|enum IN-GROUP job-attributes-tag COUNT 1 WITH-VALUE >6 REPEAT-NO-MATCH REPEAT-LIMIT 30
    DISPLAY job-state
}

{
    SKIP-IF-NOT-DEFINED HAVE_PROOF_PRINT

    NAME "[EPX2- Resubmit proof-print job"
    OPERATION Resubmit-Job

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR language attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR integer job-id $job-id
    ATTR name requesting-user-name $user

    STATUS successful-ok
}


